plugins {
    id "java"
    id "com.diffplug.spotless" version "8.0.0"
    id "io.spring.dependency-management" version "1.1.7"
    id 'org.springframework.boot' version '4.0.0'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

group = "it.chalmers"

dependencies {
    annotationProcessor(
            // Used to generate Record Builder classes
            "io.soabase.record-builder:record-builder-processor:41",
    )

    implementation(
            // Used to generate Record Builder classes
            "io.soabase.record-builder:record-builder-core:41",

            // Used to handle SQL specific errors
            "org.postgresql:postgresql:42.7.3",

            // Spring Boot
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.springframework.boot:spring-boot-starter-data-redis",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.boot:spring-boot-starter-validation",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-oauth2-authorization-server",

            // Spring session
            "org.springframework.session:spring-session-core",
            "org.springframework.session:spring-session-data-redis",

            // Redis
            "org.springframework.data:spring-data-redis",
    )

    compileOnly(
            // Testcontainers for local dev service startup
            "org.testcontainers:testcontainers:1.21.4",
            "org.testcontainers:postgresql:1.21.4",
    )

    developmentOnly(
            // Testcontainers for local dev service startup
            "org.testcontainers:testcontainers:1.21.4",
            "org.testcontainers:postgresql:1.21.4",
    )

   runtimeOnly(
            // FlywayDB (Database migration)
            "org.springframework.boot:spring-boot-starter-flyway",
            "org.flywaydb:flyway-database-postgresql",

            // Spring Boot
            "org.springframework.boot:spring-boot-devtools",
            "org.springframework.boot:spring-boot-starter-thymeleaf",

            // Thymeleaf
            "org.thymeleaf.extras:thymeleaf-extras-springsecurity6:3.1.2.RELEASE",

            // web dependencies
            "org.webjars.npm:htmx.org:1.9.12",
            "org.webjars.npm:hyperscript.org:0.9.12",
            "org.webjars.npm:picocss__pico:2.0.6",
            "org.webjars.npm:sortablejs:1.15.3"
    )
}

tasks.named("bootBuildImage") {
    environment = ["BP_JVM_VERSION": "25.*"]
}


spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
    }

    yaml {
        target "src/**/*.yml"
        jackson()
    }

    json {
        target "src/**/*.json"
        jackson()
    }
}
